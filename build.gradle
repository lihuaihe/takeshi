plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
}
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    redissonVersion = '3.23.2'
    tikaVersion = '2.8.0'
    jtsVersion = '1.19.0'
    ffmpegVersion = '6.0-1.5.9'
    javacvVersion = '1.5.9'
    mybatisPlusVersion = '3.5.3.1'
    knife4jVersion = '4.2.0'
    awsJavaSdkVersion = '1.12.520'
    lutungVersion = '0.0.8'
    firebaseVersion = '9.2.0'
    findbugsVersion = '3.0.1u2'
    libphonenumberVersion = '8.13.17'
    html2pdfVersion = '5.0.0'
    hutoolVersion = '5.8.21'
    saTokenVersion = '1.34.0'
    ognlVersion = '3.4.1'
    jaxbApiVersion = '2.3.1'
    twilioVersion = '9.9.1'
    micaAutoVersion = '3.1.1'
}

group = 'life.725'
archivesBaseName='takeshi-spring-boot-starter'
//version = '1.0.0'
version = '1.0.11-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

javadoc.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).tap {
    configureEach {
        options.encoding = 'UTF-8'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

// 不生成可运行的jar包
bootJar.enabled = false

jar {
    enabled = true
    archiveClassifier = ''
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = archivesBaseName
            pom {
                name = archivesBaseName
                description = '一个集成了各种库的，可以快速构建SpringBoot项目框架的库，GitHub地址：https://github.com/lihuaihe/takeshi'
                url = 'https://github.com/lihuaihe/takeshi'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = '七濑武【Nanase Takeshi】'
                        email = 'takeshi@725.life'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/lihuaihe/takeshi.git'
                    developerConnection = 'scm:git:ssh://github.com/lihuaihe/takeshi.git'
                    url = 'https://github.com/lihuaihe/takeshi'
                }
            }
        }
    }
    repositories {
        maven {
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-aop'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.springframework.boot:spring-boot-starter-thymeleaf'
    api 'org.springframework.retry:spring-retry'
    api "org.redisson:redisson-spring-boot-starter:${redissonVersion}"
    api "org.apache.tika:tika-core:${tikaVersion}"
    // geoPoint
    api "org.locationtech.jts:jts-core:${jtsVersion}"

    // javacv 和 ffmpeg-platform
    api "org.bytedeco:ffmpeg:${ffmpegVersion}:macosx-x86_64"
    api "org.bytedeco:ffmpeg:${ffmpegVersion}:windows-x86_64"
    api "org.bytedeco:ffmpeg:${ffmpegVersion}:linux-x86_64"
    api "org.bytedeco:javacv:${javacvVersion}"

    api "com.baomidou:mybatis-plus-boot-starter:${mybatisPlusVersion}"
    api "com.github.xiaoymin:knife4j-openapi3-jakarta-spring-boot-starter:${knife4jVersion}"
    api "com.amazonaws:aws-java-sdk-s3:${awsJavaSdkVersion}"
    api "com.amazonaws:aws-java-sdk-secretsmanager:${awsJavaSdkVersion}"
    api "com.mandrillapp.wrapper.lutung:lutung:${lutungVersion}"
    api "com.google.firebase:firebase-admin:${firebaseVersion}"
    api "com.google.code.findbugs:annotations:${findbugsVersion}"
    api "com.googlecode.libphonenumber:libphonenumber:${libphonenumberVersion}"
    api "com.itextpdf:html2pdf:${html2pdfVersion}"

    api "cn.hutool:hutool-all:${hutoolVersion}"
    api "cn.dev33:sa-token-spring-boot3-starter:${saTokenVersion}"
    api "cn.dev33:sa-token-dao-redis-jackson:${saTokenVersion}"

    api "ognl:ognl:${ognlVersion}"
    // 使用S3时提示：JAXB is unavailable. Will fallback to SDK implementation which may be less performant.If you are using Java 9+, you will need to include javax.xml.bind:jaxb-api as a dependency.
    api "javax.xml.bind:jaxb-api:${jaxbApiVersion}"

    compileOnly "com.twilio.sdk:twilio:${twilioVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "net.dreamlu:mica-auto:${micaAutoVersion}"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}
